# array manipulation methods
- There are many built-in methods in SystemVerilog to help in array searching and ordering.
- Array manipulation methods simply iterate through the array elements and each element is used to evaluate the expression specified by the `with` clause. 
- The iterator argument specifies a local variable that can be used within the with expression to refer to the current element in the iteration. 
- If an argument is not provided, `item` is the name used by default.

| methods            | description                                                            |
| ------------------ | ---------------------------------------------------------------------- |
| find()             | Returns all elements satisfying the given expression                   |
| find_index()       | Returns the indices of all elements satisfying the given expression    |
| find_first()       | Returns the first element satisfying the given expression              |
| find_first_index() | Returns the index of the first element satisfying the given expression |
| find_last()        | Returns the last element satisfying the given expression               |
| find_last_index()  | Returns the index of the last element satisfying the given expression  |

```c
module tb;
  
  int array[9] = '{1,2,3,4,5,6,7,8,9};
  int res[$] ;
  
  initial begin
    
    res = array.find(x) with (x>3);
    $display("\n");
    $display("find(x) %p" , res);
    $display("\n");
    
    res = array.find_index with (item == 4);
    $display("find_index %p" , res);
    $display("\n");
    
    res = array.find_first with (item < 5);
    $display("find_first %p" , res);
    $display("\n");
    
    res = array.find_first_index(x) with (x > 4);
    $display("find_first_index(x) %p" , res);
    $display("\n");
    
    res = array.find_last with (item <= 7 & item > 3);
    $display("find_last %p" , res);
    $display("\n");
    
    res = array.find_last_index(x) with (x <3);
    $display("find_last_index(x) %p" , res);
    $display("\n");
    
  end
endmodule
```

```txt
find(x) '{4, 5, 6, 7, 8, 9}  
  
find_index '{3}  
  
find_first '{1}  
  
find_first_index(x) '{4}  
  
find_last '{7}  
  
find_last_index(x) '{1}
```

- other methods that are optional `with` clause

| Methods        | Description                                                                                            |
| -------------- | ------------------------------------------------------------------------------------------------------ |
| min()          | Returns the element with minimum value or whose expression evaluates to a minimum                      |
| max()          | Returns the element with maximum value or whose expression evaluates to a maximum                      |
| unique()       | Returns all elements with unique values or whose expression evaluates to a unique value                |
| unique_index() | Returns the indices of all elements with unique values or whose expression evaluates to a unique value |
```c
module tb;
  
  int array[9] = '{1,2,3,4,5,6,7,8,9};
  int res[$] ;
  
  initial begin
    $display("\n");
    res = array.min();
    $display("min : %p" , res);
    
    $display("\n");
    res = array.max();
    $display("max : %p" , res);
    
    $display("\n");
    res = array.unique();
    $display("unique : %p" , res);

    $display("\n");
    res = array.unique(x) with (x < 8);
    $display("unique : %p" , res);
    
    $display("\n");
    res = array.unique_index();
    $display("unique_index : %p" , res);

	end
  
endmodule
```

```txt
min : '{1}  
  
max : '{9}  
  
unique : '{1, 2, 3, 4, 5, 6, 7, 8, 9}  
  
unique(x) : '{1, 9}

unique_index : '{0, 1, 2, 3, 4, 5, 6, 7, 8}
```

# array ordering methods

| Method    | Description                                                                           |
| --------- | ------------------------------------------------------------------------------------- |
| reverse() | Reverses the order of elements in the array                                           |
| sort()    | Sorts the array in ascending order, optionally using `with` clause                    |
| rsort()   | Sorts the array in descending order, optionally using `with` clause                   |
| shuffle() | Randomizes the order of the elements in the array. `with` clause is not allowed here. |
>[!note] ## note
>these operations change the original data of the array


```c
module tb;
  
  int array[15] = '{1,2,3,4,5,6,7,8,9,7,5,8,9,4,1};
  
  initial begin
	// reverding the array  
    array.reverse();
    $display("\n");
    $display("reverse : %p" , array);
    $display("\n");
    
    // array sorting
    array.sort();
    $display("sorted array is : %p" , array);
    $display("\n");
    
    // array sorting in desceding order
    array.rsort();
    $display("descending sorted array is : %p" , array);
    $display("\n");
    
    // shuffle the elements 
    array.shuffle();
    $display("shuffled array is : %p" , array);
    
  end
  
endmodule
```

```txt
reverse : '{1, 4, 9, 8, 5, 7, 9, 8, 7, 6, 5, 4, 3, 2, 1}  
  
sorted array is : '{1, 1, 2, 3, 4, 4, 5, 5, 6, 7, 7, 8, 8, 9, 9}  
  
descending sorted array is : '{9, 9, 8, 8, 7, 7, 6, 5, 5, 4, 4, 3, 2, 1, 1}  
  
shuffled array is : '{5, 3, 4, 4, 8, 9, 7, 1, 9, 5, 6, 1, 7, 2, 8}
```

# array reduction methods
| Method    | Description                                       |
| --------- | ------------------------------------------------- |
| sum()     | Returns the sum of all array elements             |
| product() | Returns the product of all array elements         |
| and()     | Returns the bitwise AND (&) of all array elements |
| or()      | Returns the bitwise OR (\|) of all array elements |
| xor()     | Returns the bitwise XOR (^) of all array elements |
```c
module tb;

  int array [5] = '{1,2,3,4,5};
  int res[$];
  
  initial begin
    
    $display("sum     = %0d" , array.sum());
    $display("product = %0d" , array.product());
    $display("and     = 0x%0h" , array.and());
    $display("or      = 0x%0h" , array.or());
    $display("xor     = 0x%0h" , array.xor());
 
  end
endmodule
```

```txt
sum     = 15  
product = 120  
and     = 0x0  
or      = 0x7  
xor     = 0x1
```